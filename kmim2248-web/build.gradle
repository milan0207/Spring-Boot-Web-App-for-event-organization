plugins {
    id 'war'
}

group = 'org.example'
version = 'unspecified'

repositories {
    mavenCentral()
}

war {
    archiveFileName = 'event.war'
}

dependencies {
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.16'
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    runtimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.12'
    // https://mvnrepository.com/artifact/org.freemarker/freemarker
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.33'
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.jaxrs/jackson-jaxrs-json-provider
    implementation group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: '2.18.1'
    implementation project(':kmim2248-backend')
    // https://mvnrepository.com/artifact/jakarta.servlet/jakarta.servlet-api
    compileOnly group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.1.0'
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.datatype/jackson-datatype-jsr310
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.18.1'
    // https://mvnrepository.com/artifact/com.github.jknack/handlebars
    implementation group: 'com.github.jknack', name: 'handlebars', version: '4.4.0'
}
def deploymentName = 'event'
def tomcatHome = System.getenv('CATALINA_HOME')
logger.quiet("Tomcat home detected as '${tomcatHome}'")

// ezzel a taszkkal telepítjük ki a wart a Tomcatre
tasks.register('deploy', Copy) {
    dependsOn 'war'
    // futás előtt megnézzük, hogy a Tomcat home-ja helyesen be van-e állítva
    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    // a "war" task kimenetét másoljuk
    from war.archiveFile

    // átnevezzük a war állományt a megfelelő contextRoot kialakításáért
    rename war.archiveFileName.get(), "${deploymentName}.war"

    // a másolás célpontja a Tomcat "webapps" almappája
    into "${tomcatHome}/webapps"
}

tasks.register('undeploy', Delete) {

    // futás előtt megnézzük, hogy a Tomcat home-ja helyesen be van-e állítva
    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    // töröljük a kitelepített war file-t
    delete "${tomcatHome}/webapps/${deploymentName}.war"
}